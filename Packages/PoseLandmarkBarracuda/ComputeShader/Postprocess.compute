#pragma kernel Postprocess

#include "Common.cginc"

#ifdef UPPER_BODY
    #define VERTEX_COUNT 25
#else
    #define VERTEX_COUNT 33
#endif

// Input
StructuredBuffer<float> _poseFlag;
StructuredBuffer<float> _Landmark;

// Output
RWStructuredBuffer<float4> _Output;

float Sigmoid(float x){
    return 1.0 / (1.0 + exp(-x));
}

[numthreads(VERTEX_COUNT + 1, 1, 1)]
void Postprocess(uint id : SV_DispatchThreadID)
{   
    if(id == VERTEX_COUNT){
        float isPoseExist = _poseFlag[0];
        _Output[id] = float4(isPoseExist, 0, 0, 0);
    }
    else{
        float x = _Landmark[id * 5] / IMAGE_SIZE;
        float y = 1.0 - _Landmark[id * 5 + 1] / IMAGE_SIZE;
        //z is fullbody mode only
        float z = _Landmark[id * 5 + 2] / IMAGE_SIZE;
        float visibility = _Landmark[id * 5 + 3];
        float presence = _Landmark[id * 5 + 4];

        float score = Sigmoid(min(visibility, presence));
        _Output[id] = float4(x, y, z, score);
    }
}

